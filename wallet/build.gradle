apply plugin: 'com.android.application'

ext {
    archivesBaseName = 'dash-wallet'
    androidSupportVersion = '28.0.0'
    lifecycleVersion = "2.2.0"
}
repositories {
    mavenLocal()
    maven {
        url 'http://oss.jfrog.org/artifactory/oss-snapshot-local'
        name 'OJO snapshots'
    }
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation(project(':integration-android')) {
        exclude(group: 'com.google.android', module: 'android')
    }
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.core:core:1.2.0'
    implementation('androidx.legacy:legacy-support-v13:1.0.0') {
        exclude(group: 'com.android.support', module: 'support-fragment')
        exclude(group: 'com.android.support', module: 'support-media-compat')
    }
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android:flexbox:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation "org.dashj:dashj-core:$dashjVersion"
    implementation ("org.dashevo:dashpay:0.11-SNAPSHOT") {
        exclude group: 'com.google.protobuf', module: 'protobuf-javalite'
    }
    implementation ("org.dashevo:platform-core:0.11-SNAPSHOT") {
        exclude group: 'com.google.protobuf', module: 'protobuf-javalite'
    }
    implementation 'org.dashevo:dpp:0.11-SNAPSHOT'
    implementation ("org.dashevo:dapi-client:0.11-SNAPSHOT") {
        exclude group: 'com.google.protobuf', module: 'protobuf-javalite'
    }
    implementation 'com.google.protobuf:protobuf-java:3.11.0'
    implementation 'com.google.guava:guava:27.0.1-android'
    implementation 'com.google.zxing:core:3.3.3'
    implementation "com.squareup.okhttp3:okhttp:$ok_http_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$ok_http_version"

    implementation 'com.squareup.moshi:moshi:1.9.2'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation('com.github.tony19:logback-android-classic:1.1.1-6') {
        exclude(group: 'com.google.android', module: 'android')
    }
    implementation 'com.github.tony19:logback-android-core:1.1.1-6'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.tbuonomo.andrui:viewpagerdotsindicator:4.1.2'
    implementation 'com.sambarboza:orbitview:0.2.0'
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    implementation project(path: ':common')
    implementation project(path: ':ui-redesign')

    def room_version = "2.2.5"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    //Uphold Integration
    implementation project(path: ':uphold-integration')

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.core:core-ktx:1.2.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.4"

    implementation 'com.google.firebase:firebase-analytics:17.2.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

//    Firebase inappmessaging is disabled due to a crash when dpp, dapi-client, dashpay
//    are included.  Could it be due to conflicts with the version of
//    'io.grpc', module: 'grpc-protobuf-lite': 1.21 vs 1.28?

//    implementation ('com.google.firebase:firebase-inappmessaging:19.0.1') {
//        exclude group: 'com.google.protobuf', module: 'protobuf-lite'
//        exclude group: 'com.google.firebase', module: 'protolite-well-known-types'
//    }
//    implementation ('com.google.firebase:firebase-inappmessaging-display:19.0.1') {
//        exclude group: 'com.google.protobuf', module: 'protobuf-lite'
//        exclude group: 'com.google.firebase', module: 'protolite-well-known-types'
//    }

    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation 'com.jakewharton:process-phoenix:2.0.0'

    testImplementation 'junit:junit:4.12'
    //Espresso
    androidTestImplementation ('androidx.test:runner:1.1.0') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation ('androidx.test.espresso:espresso-core:3.1.0') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation ('androidx.test.espresso:espresso-intents:3.1.0') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}


ext {
    archivesBaseName = 'dash-wallet'
}

android {
    compileSdkVersion 28

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        multiDexEnabled true
        generatedDensities = ['hdpi', 'xhdpi']
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'armeabi-v7a', 'x86', 'arm64-v8a', 'x86_64'
        }
        externalNativeBuild {
            cmake {
                cFlags '-DHAVE_CONFIG_H -DWORD=32'
                cppFlags ""
            }
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg', 'proguardTest-rules.txt'
        }
        debug {
            multiDexEnabled true
            debuggable true
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        test {
            java.srcDirs = ['test']
            resources.srcDirs = ['test']
        }
        androidTest {
            java.srcDirs = ['androidTest']
            resources.srcDirs = ['androidTest/java']
            res.srcDirs = ['androidTest/testNet3/res']
        }
        _testNet3 {
            res.srcDirs = ["testNet3/res"]
        }
        beta {
            res.srcDirs = ["beta/res"]
        }
        devNet {
            res.srcDirs = ["devNet/res"]
        }
        evonet {
            res.srcDirs = ["evonet/res"]
        }
        mobile {
            res.srcDirs = ["mobile/res"]
        }
    }
    flavorDimensions "default"
    productFlavors {
        beta {
            applicationId = "hashengineering.dash.wallet.beta"
        }
        prod {
            applicationId = "hashengineering.darkcoin.wallet"
        }
        _testNet3 {
            applicationId = "hashengineering.darkcoin.wallet_test"
        }
        devNet {
            applicationId = "hashengineering.darkcoin.wallet.devnet"
        }
        evonet {
            applicationId = "hashengineering.darkcoin.wallet.evonet"
        }
        mobile {
            applicationId = "hashengineering.darkcoin.wallet.mobile"
        }
    }
    externalNativeBuild {
        cmake {
            path 'cpp/CMakeLists.txt'
        }
    }
    packagingOptions {
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'lib/x86_64/freebsd/libscrypt.so'
        exclude 'lib/x86_64/linux/libscrypt.so'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixPatterns.gwt.xml'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixType.gwt.xml'
        exclude 'org/bitcoinj/crypto/mnemonic/wordlist/english.txt'
        exclude 'org/bitcoinj/crypto/cacerts'
        exclude 'org.bitcoin.production.checkpoints.txt'
        exclude 'org.bitcoin.test.checkpoints.txt'
        exclude 'google/protobuf/*.proto'
    }
    dexOptions {
        preDexLibraries true
    }
    dataBinding {
        enabled = true
    }
}

clean {
    file('src/main/jniLibs').deleteDir()
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
